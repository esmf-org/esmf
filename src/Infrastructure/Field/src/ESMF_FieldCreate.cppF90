! $Id: ESMF_FieldCreate.cppF90,v 1.12 2008/01/24 21:18:20 feiliu Exp $
!
! Earth System Modeling Framework
! Copyright 2002-2007, University Corporation for Atmospheric Research, 
! Massachusetts Institute of Technology, Geophysical Fluid Dynamics 
! Laboratory, University of Michigan, National Centers for Environmental 
! Prediction, Los Alamos National Laboratory, Argonne National Laboratory, 
! NASA Goddard Space Flight Center.
! Licensed under the University of Illinois-NCSA License.
!
!==============================================================================
^define ESMF_FILENAME "ESMF_FieldCreate.F90"
!
!     ESMF FieldCreate module
      module ESMF_FieldCreateMod
!
!==============================================================================
!
! This file contains the Field class methods which are automatically
!  generated from macros to handle the type/kind/rank overloading.
!  See ESMF_Field.F90 for non-macroized functions and subroutines.
!
!------------------------------------------------------------------------------
! INCLUDES
! < ignore blank lines below.  they are created by the files which
!   define various macros. >
^include "ESMF.h"
#include "ESMF_TypeKindRankMacros.hcppF90"
#include "ESMF_FieldCreateMacros.h"

!------------------------------------------------------------------------------
! !USES:
      use ESMF_UtilTypesMod
      use ESMF_BaseMod
      use ESMF_LogErrMod
      use ESMF_IOSpecMod
      use ESMF_ArraySpecMod
      use ESMF_LocalArrayMod
      use ESMF_DELayoutMod
      use ESMF_StaggerLocMod
      use ESMF_GridMod
      use ESMF_ArrayMod
      use ESMF_ArrayGetMod
      use ESMF_ArrayCreateMod
      use ESMF_FieldMod
      implicit none

!------------------------------------------------------------------------------
! !PRIVATE TYPES:
      private

!------------------------------------------------------------------------------
! !PUBLIC MEMBER FUNCTIONS:

      public ESMF_FieldCreate
      public ESMF_FieldSetCommit
 
!------------------------------------------------------------------------------
! The following line turns the CVS identifier string into a printable variable.
      character(*), parameter, private :: version = &
      '$Id: ESMF_FieldCreate.cppF90,v 1.12 2008/01/24 21:18:20 feiliu Exp $'

!==============================================================================
! 
! INTERFACE BLOCKS
!
!==============================================================================


!------------------------------------------------------------------------------
!BOPI
! !IROUTINE: ESMF_FieldCreate - Create a new Field with data
!
! !INTERFACE:
      interface ESMF_FieldCreate 
   
! !PRIVATE MEMBER FUNCTIONS:
!
        module procedure ESMF_FieldCreateNew
        module procedure ESMF_FieldCreateFromArray
        TypeKindRankInterfaceMacro(FieldCreateFromDataPtr)

! !DESCRIPTION:
!   This interface provides an entry point for methods that create a complete
!   {\tt ESMF\_Field}.  These method all contain an {\tt ESMF\_Grid} and 
!   {\tt ESMF\_Data}.  The variations allow the user to specify the data 
!   using either a Fortran array or an {\tt ESMF\_Array}.
!    
      end interface
!EOPI

!BOPI 
! !IROUTINE: ESMF_FieldSetCommit - finishes a Field started with FieldCreateEmpty
!
! !INTERFACE:
    interface ESMF_FieldSetCommit

        TypeKindRankInterfaceMacro(FieldSetCommit)

! !DESCRIPTION:
!   This interface finishes a {\tt ESMF\_Field} started with FieldCreateEmpty
!   These methods all take {\tt ESMF\_Grid} and Fortran data pointer as input to
!   fill in the {\tt ESMF\_Field} internals. 

    end interface ESMF_FieldSetCommit
!EOPI

!
!==============================================================================
!
      contains
!
!==============================================================================

!------------------------------------------------------------------------------
^undef  ESMF_METHOD
^define ESMF_METHOD "ESMF_FieldCreateNew"
!BOP
! !IROUTINE:   ESMF_FieldCreate - Create a new Field

! !INTERFACE:
      ! Private name; call using ESMF_FieldCreate()
      function ESMF_FieldCreateNew(grid, arrayspec, allocflag, staggerloc, &
                                   gridToFieldMap, ungriddedLBound, &
                                   ungriddedUBound, maxHaloLWidth, &
                                   maxHaloUWidth, name, iospec, rc)
!
! !RETURN VALUE:
      type(ESMF_Field) :: ESMF_FieldCreateNew
!
! !ARGUMENTS:
      type(ESMF_Grid) :: grid               
      type(ESMF_ArraySpec), intent(inout) :: arrayspec     
      type(ESMF_AllocFlag), intent(in), optional :: allocflag
      type(ESMF_StaggerLoc), intent(in), optional :: staggerloc 
      integer, intent(in), optional :: gridToFieldMap(:)    
      integer, intent(in), optional :: ungriddedLBound(:)
      integer, intent(in), optional :: ungriddedUBound(:)
      integer, intent(in), optional :: maxHaloLWidth(:)
      integer, intent(in), optional :: maxHaloUWidth(:)
      character (len=*), intent(in), optional :: name 
      type(ESMF_IOSpec), intent(in), optional :: iospec 
      integer, intent(out), optional :: rc              
!
! !DESCRIPTION:
!     An interface function to {\tt ESMF\_FieldCreate()}.
!     Create an {\tt ESMF\_Field} and allocate space internally for a
!     gridded {\tt ESMF\_Array}.  Return a new {\tt ESMF\_Field}.
! 
!     The arguments are:
!     \begin{description}
!     \item [grid] 
!           Pointer to an {\tt ESMF\_Grid} object. 
!     \item [arrayspec]
!           {\tt ESMF\_Data} specification. 
!     \item [{[allocflag]}]
!           Whether to allocate space for the array.  See 
!           Section~\ref{opt:allocflag} for possible values.  Default is
!           {\tt ESMF\_ALLOC}.
!     \item [{[staggerloc]}] 
!           Relative location of data per grid cell/vertex in the grid.  
!           If specified here, takes precedence over the same setting
!           in the {\tt datamap} argument.
!     \item [{[gridToFieldMap]}]
!           List that contains as many elements as is indicated by the
!           {\tt grid}'s
!           rank.  The list elements map each dimension of the Grid object to a
!           dimension in the Field's Array by specifying the appropriate Array
!           dimension index. The default is to map all of the grid's dimensions
!           against the lower dimensions of the Field's Array in sequence, i.e.
!           gridToFieldMap = (/1, 2, .../). Unmapped dimensions are undistributed
!           dimensions.  The total undistributed dimensions are the total
!           Array dimensions - the distributed dimensions in the Grid (distRank).
!           All gridToFieldMap entries must be greater than or equal to one and
!           smaller than or equal to the Array rank. It is erroneous to specify
!           the same entry multiple times
!           unless it is zero.  If the Array rank is less than the Grid dimCount then
!           the default gridToFieldMap will contain zeros for the dimCount.
!           A zero entry in the dimmap indicates that the particular Grid dimension will
!           be replicating the Array across the DEs along this direction.
!     \item [{[ungriddedLBound]}]
!           Lower bounds of the ungridded dimensions of the Field.
!     \item [{[ungriddedUBound]}]
!           Upper bounds of the ungridded dimensions of the Field.
!     \item [{[maxHaloLWidth]}]
!           Lower bound of halo region.  Defaults to 0.
!     \item [{[maxHaloUWidth]}]
!           Upper bound of halo region.  Defaults to 0.
!     \item [{[name]}] 
!           {\tt Field} name. 
!     \item [{[iospec]}] 
!           I/O specification. 
!     \item [{[rc]}] 
!           Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!     \end{description}
!
!EOP

      type(ESMF_FieldType), pointer :: ftype      ! Pointer to new field
      integer :: localrc                          !  Local error code
      logical :: rcpresent                        ! Return code present

      ! Initialize pointers
      localrc = ESMF_RC_NOT_IMPL
      rcpresent = .FALSE.
      nullify(ftype)
      nullify(ESMF_FieldCreateNew%ftypep)

      ! Initialize return code   
      if(present(rc)) then
        rcpresent=.TRUE.
        rc = ESMF_RC_NOT_IMPL
      endif

      allocate(ftype, stat=localrc)
      ! If error write message and return.
      ! Formal error handling will be added asap.
      if (ESMF_LogMsgFoundError(localrc, &
                                  ESMF_ERR_PASSTHRU, &
                                  ESMF_CONTEXT, rc)) return

      ! Call construction method to allocate and initialize field internals.
      call ESMF_FieldConstructIA(ftype, grid, arrayspec, allocflag, &
                                  staggerloc, gridToFieldMap, ungriddedLBound, &
                                  ungriddedUBound, maxHaloLWidth, &
                                  maxHaloUWidth, name, iospec, localrc)
      if (ESMF_LogMsgFoundError(localrc, &
                                  ESMF_ERR_PASSTHRU, &
                                  ESMF_CONTEXT, rc)) return
   
      ! Set return values.
      ESMF_FieldCreateNew%ftypep => ftype

      ESMF_INIT_SET_CREATED(ESMF_FieldCreateNew)
      if(rcpresent) rc = ESMF_SUCCESS

      end function ESMF_FieldCreateNew

!------------------------------------------------------------------------------
^undef  ESMF_METHOD
^define ESMF_METHOD "ESMF_FieldCreateFromArray"
!BOP
! !IROUTINE: ESMF_FieldCreate - Create a Field from an existing ESMF Array

! !INTERFACE:
      ! Private name; call using ESMF_FieldCreate()
      function ESMF_FieldCreateFromArray(grid, array, copyflag, staggerloc, &
                                         gridToFieldMap, ungriddedLBound, &
                                         ungriddedUBound, maxHaloLWidth, &
                                         maxHaloUWidth, name, &
                                         iospec, rc)
!
! !RETURN VALUE:
      type(ESMF_Field) :: ESMF_FieldCreateFromArray    
!
! !ARGUMENTS:
      type(ESMF_Grid), intent(in) :: grid                
      type(ESMF_Array), intent(in) :: array              
      type(ESMF_CopyFlag), intent(in), optional :: copyflag       
      type(ESMF_StaggerLoc), intent(in), optional :: staggerloc 
      integer, intent(in), optional :: gridToFieldMap(:)    
      integer, intent(in), optional :: ungriddedLBound(:)
      integer, intent(in), optional :: ungriddedUBound(:)
      integer, intent(in), optional :: maxHaloLWidth(:)
      integer, intent(in), optional :: maxHaloUWidth(:)
      character (len = *), intent(in), optional :: name   
      type(ESMF_IOSpec), intent(in), optional :: iospec   
      integer, intent(out), optional :: rc                
!
! !DESCRIPTION:
!     An interface function to {\tt ESMF\_FieldCreate()}.
!     This version of creation assumes the data exists already and is being
!     passed in through an {\tt ESMF\_Array}.  
! 
!     The arguments are:
!     \begin{description}
!     \item [grid] 
!           Pointer to an {\tt ESMF\_Grid} object. 
!     \item [array]
!           Includes data specification and allocated memory. 
!           It must already include space for the halo regions.
!     \item [{[copyflag]}]
!           Indicates whether to reference the array or make a 
!           copy of it.  Valid values are {\tt ESMF\_DATA\_COPY} and 
!           {\tt ESMF\_DATA\_REF} (default), respectively.
!     \item [{[staggerloc]}] 
!           Relative location of data per grid cell/vertex in the grid.
!     \item [{[gridToFieldMap]}]
!           List that contains as many elements as is indicated by the
!           {\tt grid}'s
!           rank.  The list elements map each dimension of the Grid object to a
!           dimension in the Field's Array by specifying the appropriate Array
!           dimension index. The default is to map all of the grid's dimensions
!           against the lower dimensions of the Field's Array in sequence, i.e.
!           gridToFieldMap = (/1, 2, .../). Unmapped dimensions are undistributed
!           dimensions.  The total undistributed dimensions are the total
!           Array dimensions - the distributed dimensions in the Grid (distRank).
!           All gridToFieldMap entries must be greater than or equal to one and
!           smaller than or equal to the Array rank. It is erroneous to specify
!           the same entry multiple times
!           unless it is zero.  If the Array rank is less than the Grid dimCount then
!           the default gridToFieldMap will contain zeros for the dimCount.
!           A zero entry in the dimmap indicates that the particular Grid dimension will
!           be replicating the Array across the DEs along this direction.
!     \item [{[ungriddedLBound]}]
!           Lower bounds of the ungridded dimensions of the Field.
!     \item [{[ungriddedUBound]}]
!           Upper bounds of the ungridded dimensions of the Field.
!     \item [{[maxHaloLWidth]}]
!           Lower bound of halo region.  Defaults to 0.
!     \item [{[maxHaloUWidth]}]
!           Upper bound of halo region.  Defaults to 0.
!     \item [{[name]}] 
!           {\tt Field} name. 
!     \item [{[iospec]}] 
!           I/O specification. 
!     \item [{[rc]}] 
!           Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!     \end{description}
!
!EOP

      type(ESMF_FieldType), pointer :: ftype  ! Pointer to new field
      integer :: localrc                       !  Local error code
      logical :: rcpresent                    ! Return code present
      
      ! Initialize pointers
      localrc = ESMF_RC_NOT_IMPL
      rcpresent = .FALSE.
      nullify(ftype)
      nullify(ESMF_FieldCreateFromArray%ftypep)

      ! Initialize return code   
      if(present(rc)) then
        rcpresent = .TRUE. 
        rc = ESMF_RC_NOT_IMPL
      endif     

      allocate(ftype, stat=localrc)
      if (ESMF_LogMsgFoundAllocError(localrc, "Allocating Field information", &
                                       ESMF_CONTEXT, rc)) return

      ! Call construction method to allocate and initialize field internals.
      call ESMF_FieldConstructIA(ftype, grid, array, copyflag, staggerloc, &
                                       gridToFieldMap, ungriddedLBound, &
                                       ungriddedUBound, maxHaloLWidth, &
                                       maxHaloUWidth, name, &
                                       iospec, localrc)
      if (ESMF_LogMsgFoundError(localrc, &
                                  ESMF_ERR_PASSTHRU, &
                                  ESMF_CONTEXT, rc)) return
   

      ! Set return values.
      ESMF_FieldCreateFromArray%ftypep => ftype
      ESMF_INIT_SET_CREATED(ESMF_FieldCreateFromArray)
      if(rcpresent) rc = ESMF_SUCCESS

      end function ESMF_FieldCreateFromArray

!------------------------------------------------------------------------------

      ! < declarations of subroutines for each T/K/R >

TypeKindRankDeclarationMacro(FieldSetCommit)
TypeKindRankDeclarationMacro(FieldCreateFromDataPtr)

!------------------------------------------------------------------------------


      end module ESMF_FieldCreateMod

